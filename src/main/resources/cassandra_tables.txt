CREATE KEYSPACE hotels WITH replication = {'class': 'SimpleStrategy', 'replication_factor': '3'}  AND durable_writes = true;

CREATE TYPE hotels.address (
    street text,
    city text,
    state_or_province text,
    postal_code text,
    country text
);

CREATE TABLE hotels.reservations_by_confirmation (
    confirmation_number text PRIMARY KEY,
    end_date date,
    guest_id uuid,
    hotel_id text,
    room_number smallint,
    start_date date
) ;

CREATE TABLE hotels.available_rooms_by_hotel_id_and_date (
    hotel_id text,
    date date,
    room_number smallint,
    is_available boolean,
    PRIMARY KEY (hotel_id, date, room_number)
);

CREATE TABLE hotels.amenities_by_room (
    hotel_id text,
    room_number smallint,
    amenity_name text,
    description text,
    PRIMARY KEY ((hotel_id, room_number), amenity_name)
);

CREATE TABLE hotels.guests_by_id (
    guest_id uuid PRIMARY KEY,
    addresses map<text, frozen<address>>,
    emails set<text>,
    first_name text,
    last_name text,
    phones list<text>,
    title text
);

CREATE TABLE hotels.hotels_by_id (
    id text PRIMARY KEY,
    address frozen<address>,
    name text,
    phone text,
    pois set<text>
);

CREATE TABLE hotels.hotels_by_poi (
    poi_name text,
    hotel_id text,
    address frozen<address>,
    name text,
    phone text,
    poi_description text static,
    PRIMARY KEY (poi_name, hotel_id)
);

CREATE TABLE hotels.reservations_by_hotel_id_and_date (
    hotel_id text,
    start_date date,
    room_number smallint,
    confirmation_number text,
    end_date date,
    guest_id uuid,
    PRIMARY KEY ((hotel_id, start_date), room_number)
);

CREATE TABLE hotels.reservations_by_guest_last_name (
    guest_last_name text,
    guest_id uuid,
    confirmation_number text,
    end_date date,
    hotel_id text,
    room_number smallint,
    start_date date,
    PRIMARY KEY (guest_last_name, guest_id, confirmation_number)
);

